.template 0
#   Copyright (C) 2015-2020 Virgil Security, Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   Perform resolution for item 'features' and all children.
#
#   Resolution includes several steps:
#       1. Validate - check that required attributes and items are defined.
#       2. Define default values to the optional component attributes.
#       3. Append optional items.
#
#    Private function prefix is: D25.
#
# ---------------------------------------------------------------------------
#   This is a code generator built using the iMatix GSL code generation
#   language. See https://github.com/zeromq/gsl for details.
# ---------------------------------------------------------------------------

gsl from "common.gsl"
gsl from "component.gsl"


# ---------------------------------------------------------------------------
#   Check required attribute: path.
# ---------------------------------------------------------------------------
function _D25_require_attr_path (item, caller)
    check_arguments (my, "item", my)

    assert_attribute (my.item, "path", my.caller ? my)
endfunction


# ---------------------------------------------------------------------------
#   Resolve attribute: uid.
# ---------------------------------------------------------------------------
function _D25_resolve_attr_uid (feature, parent)
    check_arguments (my, "feature, parent", my)

    component_require_landlord (my.feature, my)

    my.feature.uid = make_id (component_derive_landlord_uid (my.feature), name (my.feature), my.feature.name)
endfunction


# ---------------------------------------------------------------------------
#   Resolve attribute: features.prefix.
# ---------------------------------------------------------------------------
function _D25_resolve_attr_prefix (item, parent)
    check_arguments (my, "item", my)

    my.item.prefix ?= my.parent.prefix ? my.item->source.name
endfunction

# ---------------------------------------------------------------------------
#   Resolve attribute: feature.default.
# ---------------------------------------------------------------------------
function _D25_resolve_attr_default (item, parent)
    check_arguments (my, "item", my)

    my.item.default ?= "on"
endfunction

# ---------------------------------------------------------------------------
#   Resolve component: feature.
# ---------------------------------------------------------------------------
function _D25_resolve_feature (feature, parent)
    check_arguments (my, "feature, parent", my)

    component_require_attr_name (my.feature, my)
    component_resolve_landlord (my.feature, my.parent)

    _D25_resolve_attr_prefix (my.feature, my.parent)
    _D25_resolve_attr_default (my.feature, my.parent)
    _D25_resolve_attr_uid (my.feature, my.parent)

    for my.feature.require
        component_resolve_require (require, my.feature)
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Resolve component: feature.
# ---------------------------------------------------------------------------
function _D25_resolve_require (require, parent)
    check_arguments (my, "require, parent", my)

    component_resolve_landlord (my.require, my.parent)

    for my.require.alternative
        component_resolve_alternative (alternative, my.require)
    endfor
endfunction


# ---------------------------------------------------------------------------
#   Resolve component: features.
# ---------------------------------------------------------------------------
function features_resolve (features, parent)
    check_arguments (my, "features, parent", my)

    assert_attribute_one_of (my.features, "project, library", my)

    _D25_require_attr_path (my.features, my)
    _D25_resolve_attr_prefix (my.features)

    component_resolve_landlord (my.features, my.parent)

    for my.features.feature
        _D25_resolve_feature (feature, my.features)
    endfor

    for my.features.require
        _D25_resolve_require (require, my.features)
    endfor

endfunction

# ---------------------------------------------------------------------------
#   Resolve all 'features' entries within 'source'
# ---------------------------------------------------------------------------
function foreach_features_resolve (source)
    check_arguments (my, "source", my)

    for my.source.features
        features_resolve (features, my.source)
    endfor
endfunction


# ---------------------------------------------------------------------------
#   Load existing <features_list/> entity or create new one.
# ---------------------------------------------------------------------------
function features_list_load (work_path)
    my.dir_path = directory.resolve (my.work_path) ?
    my.file_path = "$(my.dir_path?:)features_list.xml"

    if file.exists (my.file_path)
        my.features_list = item_load_from_file(my.file_path)
    else
        my.features_list = XML.new("features_list")
    endif

    return my.features_list
endfunction


# ---------------------------------------------------------------------------
#   Save <features_list/> entity to the file at given path.
# ---------------------------------------------------------------------------
function features_list_save (features_list, work_path)
    check_arguments (my, "features_list", my)

    item_save_to_file(my.features_list, "features_list.xml", my.work_path)
endfunction


# ---------------------------------------------------------------------------
#   Inspect library features and add them to the list.
# ---------------------------------------------------------------------------
function features_list_update_from_library (features_list, library)
    check_arguments (my, "features_list, library", my)

    echo_trace ("Create <features/> for <library name=\"$(my.library.name)\"", my)

    #   Clean first
    for my.features_list.features where string_equal(0.library ?, my.library.name)
        delete features
    endfor

    #   Grab features
    new features to my.features_list
        .prefix = my.library.prefix ?
        .path = my.library.path
        .library = my.library.name

        my.library_feature = my.library->feature(feature.name = "library") ?
        if !defined(my.library_feature)
            new feature to features
                .name = "library"
                my.library_feature = feature
            endnew
        endif
        my.library_feature. ?= "Enable build of the '$(my.library.name:)' library"


        for my.library.feature
            copy feature to features
        endfor

        for my.library.require
            copy require to features
        endfor
    endnew
endfunction


# ---------------------------------------------------------------------------
#   Inspect project features and add them to the list.
# ---------------------------------------------------------------------------
function features_list_update_from_project (features_list, project)
    check_arguments (my, "features_list, project", my)

    echo_trace ("Create <features/> for <project name=\"$(my.project.name)\"", my)

    #   Clean first
    for my.features_list.features where string_equal(0.project ?, my.project.name)
        delete features
    endfor

    #   Grab features
    new features to my.features_list
        .prefix = my.project.prefix ?
        .path = my.project.lib_path
        .project = my.project.name
        .cmake_target = my.project.cmake_target


        my.library_feature = my.project->feature(feature.name = "library") ?
        if !defined(my.library_feature)
            new feature to features
                .name = "library"
                my.library_feature = feature
            endnew
        endif
        my.library_feature. ?= "Enable build of the '$(my.project.name:)' library"

        for my.project.cmake_compile_definition
            copy cmake_compile_definition to features
        endfor

        for my.project.feature
            copy feature to features
        endfor

        for my.project.require
            copy require to features
        endfor
    endnew
endfunction

.endtemplate
