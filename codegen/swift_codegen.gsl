.template 0
#   Copyright (C) 2015-2021 Virgil Security, Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   Functions for Swift primitives generation: protocol, class, enum, etc.
# ---------------------------------------------------------------------------
#   This is a code generator built using the iMatix GSL code generation
#   language. See https://github.com/zeromq/gsl for details.
# ---------------------------------------------------------------------------


gsl from "common.gsl"

# ===========================================================================
#   Format.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Return formatted list of arguments without braces.
# ---------------------------------------------------------------------------
function swift_format_argument_list (swift_method)
    check_arguments (my, "swift_method", my)

    for my.swift_method.swift_argument
        my.arg_list ?= ""
        my.arg_list += "$(swift_argument.ext_name + " "?:)$(swift_argument.name:): $(swift_argument.type:)"
        if ! last()
            my.arg_list += ", "
        endif
    endfor

    return my.arg_list
endfunction

# ---------------------------------------------------------------------------
#   Return formatted list of returned value(s).
# ---------------------------------------------------------------------------
function swift_format_return_list (swift_method)
    check_arguments (my, "swift_method", my)

    my.is_tuple = count (my.swift_method.swift_return) > 1 ?? "1" ? "0"

    for my.swift_method.swift_return
        my.return_list ?= ""

        if my.is_tuple
            my.return_list += "$(swift_return.name:): $(swift_return.type:)"
        else
            my.return_list += "$(swift_return.type:)"
        endif

        if ! last()
            my.return_list += ", "
        endif
    endfor

    if defined (my.return_list) & my.is_tuple
        my.return_list = "(" + my.return_list + ")"
    endif

    if defined (my.return_list)
        my.return_list = " -> " + my.return_list
    endif

    return my.return_list
endfunction

# ---------------------------------------------------------------------------
#   Return inheritance list if defined <swift_inherit/>, otherwise - nothing.
# ---------------------------------------------------------------------------
function swift_format_inheritance_list (swift_type)
    check_arguments (my, "swift_type", my)

     for my.swift_type.swift_inherit
        my.inheritance_list ?= ": "
        my.inheritance_list += swift_inherit.type
        if ! last ()
            my.inheritance_list += ", "
        endif
    endfor

    return my.inheritance_list
endfunction


# ===========================================================================
#   Generate.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Insert given multiline code.
# ---------------------------------------------------------------------------
function swift_insert_code (code, tab_count)
    check_argument (my, "code", my)

    my.code = "$(my.code:left,no)"
    my.code = string_trim (my.code)
    my.code = code_collapse_spaces (my.code)

    my.code = string_indent (my.code, my.tab_count)
    my.code = string_multi_rtrim (my.code)

    out (my.code)
endfunction

# ---------------------------------------------------------------------------
#   Insert description of the given object.
# ---------------------------------------------------------------------------
function swift_insert_description (obj, tab_count)
    my.desciption = my.obj. ?
    my.tab_count ?= 0
    if string_empty (my.desciption)
        return
    endif
    swift_insert_code (my.desciption, my.tab_count)
endfunction

# ---------------------------------------------------------------------------
#   Return formatted multiline commented string.
# ---------------------------------------------------------------------------
function swift_format_description (obj)
    if ! defined (my.obj.)
        return
    endif

    my.str = my.obj.
    my.str = string_indent (my.str)
    my.str = "/// $(my.str:left, block, no)$(terminator)"
    my.str = string_multi_rtrim (my.str)

    return my.str
endfunction

# ---------------------------------------------------------------------------
#   Return multiline formatted string with license.
# ---------------------------------------------------------------------------
function swift_format_license(license)
    return swift_format_description (my.license) ?
endfunction

# ---------------------------------------------------------------------------
#   Insert: license + caution + description
# ---------------------------------------------------------------------------
function swift_insert_prologue (swift_module)
    check_arguments (my, "swift_module", my)

    assert_item (my.swift_module, "swift_license", my)

    swift_insert_description (my.swift_module->swift_license)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: import
# ---------------------------------------------------------------------------
function swift_insert_import (swift_import)
    check_arguments (my, "swift_import", my)

    assert_attribute (my.swift_import, "framework", my)

    swift_insert_code ("import $(my.swift_import.framework:)")
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: protocol.property
# ---------------------------------------------------------------------------
function swift_insert_protocol_property (swift_property)
    check_arguments (my, "swift_property", my)

    assert_attributes (my.swift_property, "name, type, visibility", my)

    my.property_modifiers = (my.swift_property.access = "readonly") ?? "{ get }" ? "{ get set }"

    swift_insert_description (my.swift_property, 1)
    swift_insert_code("\
        @objc $(my.swift_property.modifier?:) var $(my.swift_property.name:): \
                $(my.swift_property.type:) $(my.property_modifiers:)
    ", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: enum.case
# ---------------------------------------------------------------------------
function swift_insert_enum_constant (swift_constant)
    check_arguments (my, "swift_constant", my)

    my.value = defined (my.swift_constant.value) ?? " = $(my.swift_constant.value:)" ? ""
    swift_insert_description (my.swift_constant, 1)
    swift_insert_code ("\
        case $(my.swift_constant.name:)$(my.value:)
    ", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: constructor / init
# ---------------------------------------------------------------------------
function swift_insert_constructor (swift_constructor)
    check_arguments (my, "swift_constructor", my)

    assert_attributes (my.swift_constructor, "visibility", my)
    assert_item (my.swift_constructor, "swift_code", my)

    my.method_args = swift_format_argument_list(my.swift_constructor) ?
    my.override = is_true (my.swift_constructor.override) ?? "override" ?

    swift_insert_description (my.swift_constructor, 1)
    swift_insert_code("\
         $(my.swift_constructor.visibility:) $(my.override?:) init($(my.method_args?:)) {
    ", 1)

    swift_insert_code("\
        $(my.swift_constructor->swift_code.:)
    ", 2)

    swift_insert_code ("}", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: destructor / deinit
# ---------------------------------------------------------------------------
function swift_insert_destructor (swift_destructor)
    check_arguments (my, "swift_destructor", my)

    swift_insert_description (my.swift_destructor, 1)
    swift_insert_code("\
         deinit {
    ", 1)

    swift_insert_code("\
        $(my.swift_destructor->swift_code.:)
    ", 2)

    swift_insert_code ("}", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: protocol.method
# ---------------------------------------------------------------------------
function swift_insert_protocol_method (swift_method)
    check_arguments (my, "swift_method", my)

    assert_attributes (my.swift_method, "name", my)

    my.method_args = swift_format_argument_list(my.swift_method) ?
    my.method_return = swift_format_return_list(my.swift_method) ?
    my.method_throws = my.swift_method.throws ?? "throws" ?
    my.method_objc = my.swift_method.objc ?? "@objc" ?

    swift_insert_description (my.swift_method, 1)
    swift_insert_code("\
        $(my.method_objc?:) $(my.swift_method.modifier?:) func $(my.swift_method.name:)($(my.method_args?:)) \
                $(my.method_throws?:) $(my.method_return?:)
    ", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: class.constant
# ---------------------------------------------------------------------------
function swift_insert_class_constant (swift_constant)
    check_arguments (my, "swift_constant", my)

    swift_insert_description (my.swift_constant, 1)
    swift_insert_code ("\
        @objc $(my.swift_constant.visibility:) static let $(my.swift_constant.name:): $(my.swift_constant.type:) = \
             $(my.swift_constant.value:)
    ", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: class.property
# ---------------------------------------------------------------------------
function swift_insert_class_property (swift_property)
    check_arguments (my, "swift_property", my)

    assert_attributes (my.swift_property, "name, type, visibility", my)

    my.is_computed = count (my.swift_property.computed)
    my.is_const = my.swift_property.access = "readonly"
    my.is_writeonly = my.swift_property.access = "writeonly"

    my.let_var = (my.is_const & !my.is_computed) ?? "let" ? "var"

    if my.is_computed
        my.var_init =  " {"
    elsif defined (my.swift_property.value)
        my.var_init =  " = $(my.swift_property.value:)"
    endif

    swift_insert_description (my.swift_property, 1)
    swift_insert_code("\
        @objc $(my.swift_property.visibility:) $(my.swift_property.modifier?:) $(my.let_var:) \
                $(my.swift_property.name:): $(my.swift_property.type:)$(my.var_init?:)
    ", 1)

    if my.is_computed
        swift_insert_code("\
            $(my.swift_property->computed.)
        ", 2)
        swift_insert_code("}", 1)
    endif
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: class.method
# ---------------------------------------------------------------------------
function swift_insert_class_method (swift_method)
    check_arguments (my, "swift_method", my)

    assert_attributes (my.swift_method, "name", my)

    my.method_args = swift_format_argument_list(my.swift_method) ?
    my.method_return = swift_format_return_list(my.swift_method) ?
    my.method_throws = is_true (my.swift_method.throws) ?? "throws" ?
    my.override = is_true (my.swift_method.override) ?? "override" ?
    my.method_objc = my.swift_method.objc ?? "@objc" ?

    swift_insert_description (my.swift_method, 1)
    swift_insert_code("\
        $(my.method_objc?:) $(my.swift_method.visibility?:) $(my.swift_method.modifier?:) $(my.override?:) \
                func $(my.swift_method.name:)($(my.method_args?:)) $(my.method_throws?:) $(my.method_return?:) {
    ", 1)

    swift_insert_code (my.swift_method->swift_code., 2)

    swift_insert_code ("}", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: class.method
# ---------------------------------------------------------------------------
function swift_insert_enum_method (swift_method)
    check_arguments (my, "swift_method", my)

    assert_attributes (my.swift_method, "name", my)

    my.method_args = swift_format_argument_list(my.swift_method) ?
    my.method_return = swift_format_return_list(my.swift_method) ?
    my.method_throws = is_true (my.swift_method.throws) ?? "throws" ?

    swift_insert_description (my.swift_method, 1)
    swift_insert_code("\
        $(my.swift_method.visibility?:) $(my.swift_method.modifier?:) func \
                $(my.swift_method.name:)($(my.method_args?:)) $(my.method_throws?:) $(my.method_return?:) {
    ", 1)

    swift_insert_code (my.swift_method->swift_code., 2)

    swift_insert_code ("}", 1)
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: protocol
# ---------------------------------------------------------------------------
function swift_insert_protocol (swift_protocol)
    check_arguments (my, "swift_protocol", my)

    echo_trace ("Generate swift protocol: " + my.swift_protocol.name, my)

    assert_attributes (my.swift_protocol, "name, objc_name, visibility", my)

    for my.swift_protocol.swift_inherit
        my.swift_inheritance_list ?= ": "
        my.swift_inheritance_list += swift_inherit.type
        if ! last()
            my.swift_inheritance_list += ", "
        endif
    endfor

    swift_insert_description(my.swift_protocol)
    swift_insert_code ("\
        @objc($(my.swift_protocol.objc_name:)) $(my.swift_protocol.visibility:) protocol $(my.swift_protocol.name:) \
                $(my.swift_inheritance_list?:) {
    ")

    for my.swift_protocol.swift_property
        swift_insert_protocol_property (swift_property)
    endfor

    for my.swift_protocol.swift_method
        newline ()
        swift_insert_protocol_method (swift_method)
    endfor

    swift_insert_code ("}")
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: class
# ---------------------------------------------------------------------------
function swift_insert_class (swift_class)
    check_arguments (my, "swift_class", my)

    echo_trace ("Generate Swift class: " + my.swift_class.name, my)

    assert_attributes (my.swift_class, "name, objc_name, visibility", my)

    my.swift_inheritance_list = swift_format_inheritance_list (my.swift_class) ?

    swift_insert_description(my.swift_class)
    swift_insert_code ("\
        @objc($(my.swift_class.objc_name:)) $(my.swift_class.visibility:) \
                class $(my.swift_class.name:)$(my.swift_inheritance_list?:) {
    ")

    for my.swift_class.swift_constant
        newline_if (first ())
        swift_insert_class_constant (swift_constant)
    endfor

    for my.swift_class.swift_property
        newline ()
        swift_insert_class_property (swift_property)
    endfor

    for my.swift_class.swift_constructor
        newline ()
        swift_insert_constructor (swift_constructor)
    endfor

    for my.swift_class.swift_destructor
        newline ()
        swift_insert_destructor (swift_destructor)
    endfor

    for my.swift_class.swift_method
        newline ()
        swift_insert_class_method (swift_method)
    endfor

    swift_insert_code ("}")
endfunction

# ---------------------------------------------------------------------------
#   Insert statement: enum
# ---------------------------------------------------------------------------
function swift_insert_enum (swift_enum)
    check_arguments (my, "swift_enum", my)

    echo_trace ("Generate Swift enum: " + my.swift_enum.name, my)

    assert_attributes (my.swift_enum, "name, objc_name, visibility", my)

    my.swift_inheritance_list = swift_format_inheritance_list (my.swift_enum) ?

    swift_insert_description(my.swift_enum)
    swift_insert_code ("\
        @objc($(my.swift_enum.objc_name:)) $(my.swift_enum.visibility:)\
                enum $(my.swift_enum.name:)$(my.swift_inheritance_list?:) {
    ")

    for my.swift_enum.swift_constant
        newline ()
        swift_insert_enum_constant (swift_constant)
    endfor

    for my.swift_enum.swift_constructor
        newline ()
        swift_insert_constructor (swift_constructor)
    endfor

    for my.swift_enum.swift_method
        newline ()
        swift_insert_enum_method (swift_method)
    endfor

    swift_insert_code ("}")

endfunction

# ---------------------------------------------------------------------------
#   Generate project files.
# ---------------------------------------------------------------------------
function swift_generate_project (swift_project)
    check_arguments (my, "swift_project", my)

endfunction

# ---------------------------------------------------------------------------
#   Generate source files from <swift_module/> entities.
# ---------------------------------------------------------------------------
function swift_generate_sources (swift_project)
    check_arguments (my, "swift_project", my)

    assert_attribute (my.swift_project, "source_dir", my)

    if -1 = directory.create (my.swift_project.source_dir)
        echo_fatal ("Can not create directory: $(my.swift_project.source_dir:)")
    endif

    for my.swift_project.swift_module
        echo_trace ("Generate Swift files from <$(name (swift_module)) name = \"$(swift_module.name:)\"/>", my)

        output swift_module.source_file_path

        swift_insert_prologue (swift_module)
        newline ()

        for swift_module.swift_import
            newline_if (first ())
            swift_insert_import (swift_import)
        endfor

        for swift_module.swift_enum
            newline ()
            swift_insert_enum (swift_enum)
        endfor

        for swift_module.swift_protocol
            newline ()
            swift_insert_protocol (swift_protocol)
        endfor

        for swift_module.swift_class
            newline ()
            swift_insert_class (swift_class)
        endfor

        for swift_module.swift_extension
            newline ()
            swift_insert_extension (swift_extension)
        endfor
    endfor
endfunction

.endtemplate
